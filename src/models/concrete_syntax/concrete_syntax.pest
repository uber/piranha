// Concrete Syntax Grammar for Piranha
concrete_syntax = { SOI ~ pattern ~ EOI }
pattern = { (element)+ ~ ("|>" ~ constraints)? }

// An element is either a capture or literal text
element = _{ capture | literal_text }

// Captures: :[name], :[name+], :[name*], @name
capture = { (":[" ~ identifier ~ capture_mode? ~ "]") | "@"~identifier } // FIXME: Should remove @ from the grammar, because literals may be parsed incorrectly
capture_mode = { "+" | "*" }
identifier = { (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

// Literal text - single word/token without whitespace
literal_text = { (!( ":[" | "|>" | "@" ) ~ ANY)+ }
WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }

// Where constraints (extensible for future constraint types)
constraints = { constraint ~ ("," ~ constraint)* }
constraint = { in_constraint }
in_constraint = { capture ~ "in" ~ "[" ~ list_items? ~ "]" }
list_items = { quoted_string ~ ("," ~ quoted_string)* }
quoted_string = { "\"" ~ ( "\\\\" | "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
